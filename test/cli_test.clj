(ns cli-test
  "The simpleviz CLI run as a real process (`bb --config <repo>/bb.edn -m
  cli ...`) from a temp folder, as the launcher and the jar run it."
  (:require [babashka.fs :as fs]
            [babashka.process :as p]
            [cheshire.core :as json]
            [cli]
            [clojure.string :as str]
            [clojure.test :refer [deftest is]]
            [proc-util]
            [simpleviz.main]))

(defn- run-cli
  "Run the CLI with `args` in `dir` (default: a fresh temp folder);
  {:out :err :exit}."
  [args & {:keys [dir env]}]
  (let [tmp (or dir (fs/create-temp-dir {:prefix "cli-test"}))]
    (try
      (select-keys
       (apply p/shell {:dir (str tmp) :out :string :err :string :continue true :extra-env (or env {})}
              "bb" "--config" (str proc-util/repo-root "/bb.edn") "-m" "cli" args)
       [:out :err :exit])
      (finally (when-not dir (fs/delete-tree tmp))))))

(defn- with-tmp [f]
  (let [tmp (fs/create-temp-dir {:prefix "cli-test"})]
    (try (f tmp) (finally (fs/delete-tree tmp)))))

(deftest main-finds-the-jar-behind-a-resource-url
  ;; bb's resource URLs keep the path as is; the JVM's are percent-encoded
  (is (= "/opt/a b/simpleviz.jar"
         (simpleviz.main/jar-path "jar:file:/opt/a b/simpleviz.jar!/simpleviz/main.clj")))
  (is (= "/opt/a b/simpleviz.jar"
         (simpleviz.main/jar-path "jar:file:/opt/a%20b/simpleviz.jar!/simpleviz/main.clj")))
  (is (nil? (simpleviz.main/jar-path "file:/repo/server/simpleviz/main.clj"))))

(deftest main-in-a-checkout-runs-the-cli-in-place
  ;; bb.edn loaded, but simpleviz.main is no jar entry: nothing to re-run
  (let [tmp (fs/create-temp-dir {:prefix "cli-test"})]
    (try
      (let [res (p/shell {:dir (str tmp) :out :string :err :string :continue true}
                         "bb" "--config" (str proc-util/repo-root "/bb.edn") "-m" "simpleviz.main" "--version")]
        (is (= 0 (:exit res)) (:err res))
        (is (= "simpleviz dev" (str/trim (:out res)))))
      (finally (fs/delete-tree tmp)))))

(deftest help-prints-usage
  (doseq [args [[] ["--help"] ["-h"]]]
    (let [res (run-cli args)]
      (is (= 0 (:exit res)))
      (is (str/starts-with? (:out res) "usage: simpleviz")))))

(deftest unknown-option-is-a-usage-error
  (let [res (run-cli ["-x"])]
    (is (= 1 (:exit res)))
    (is (str/includes? (:err res) "usage: simpleviz"))))

(deftest version-in-a-checkout-is-dev
  (is (= "simpleviz dev" (str/trim (:out (run-cli ["--version"]))))))

(deftest update-names-the-bbin-command
  (let [res (run-cli ["update"])]
    (is (= 0 (:exit res)))
    (is (str/includes? (:out res) "bbin install https://github.com/sstoehrm/simpleviz/releases/latest/download/simpleviz.jar"))))

(deftest clean-all-needs-the-launcher
  (let [res (run-cli ["clean-all"])]
    (is (= 1 (:exit res)))
    (is (= "simpleviz: clean-all needs the install.sh launcher (Linux)" (str/trim (:err res))))))

(deftest init-writes-a-clean-starter-and-refuses-to-overwrite
  (with-tmp
    (fn [tmp]
      (let [res (run-cli ["init" "g.edn"] :dir tmp)]
        (is (= 0 (:exit res)) (:err res))
        (is (= "created g.edn — view it with: simpleviz g.edn" (str/trim (:out res)))))
      (is (= "ok" (str/trim (:out (run-cli ["check" "g.edn"] :dir tmp)))))
      (let [res (run-cli ["init" "g.edn"] :dir tmp)]
        (is (= 1 (:exit res)))
        (is (str/includes? (:err res) "g.edn already exists"))))))

(deftest fork-and-promote-work-on-relative-paths
  (with-tmp
    (fn [tmp]
      (spit (str (fs/path tmp "g.edn")) "{:nodes {:a {}}}")
      (let [res (run-cli ["fork" "g.edn" "next"] :dir tmp)]
        (is (= 0 (:exit res)) (:err res))
        (is (fs/exists? (fs/path tmp "g-next.edn"))))
      (spit (str (fs/path tmp "g-next.edn")) "{:nodes {:a {} :b {}}}")
      (is (= 0 (:exit (run-cli ["promote" "g.edn" "next"] :dir tmp))))
      (is (str/includes? (slurp (str (fs/path tmp "g.edn"))) ":b")))))

(deftest fork-rejects-wrong-arg-count-and-missing-file
  (let [res (run-cli ["fork" "g.edn"])]
    (is (= 1 (:exit res)))
    (is (str/includes? (:err res) "usage:")))
  (let [res (run-cli ["promote" "nope.edn" "next"])]
    (is (= 1 (:exit res)))
    (is (str/includes? (:err res) "file not found: nope.edn"))))

(deftest extract-prints-the-embedded-edn
  (let [res (run-cli ["extract" (str proc-util/repo-root "/test/fixtures/embedded.png")])]
    (is (= 0 (:exit res)) (:err res))
    (is (str/includes? (:out res) ":nodes"))))

(deftest extract-reads-an-svg-export
  (let [svg (str proc-util/repo-root "/test/fixtures/compare.svg")]
    (let [res (run-cli ["extract" svg])]
      (is (= 0 (:exit res)) (:err res))
      (is (= "{:nodes {:a {} :b {}}}" (:out res))))
    (let [res (run-cli ["extract" svg "--old"])]
      (is (= 0 (:exit res)) (:err res))
      (is (= "{:nodes {:a {}}}" (:out res))))))

(deftest extract-refuses-a-file-that-is-neither-png-nor-svg
  (let [res (run-cli ["extract" (str proc-util/repo-root "/examples/demo.edn")])]
    (is (= 1 (:exit res)))
    (is (str/includes? (str (:out res) (:err res)) "is not a PNG or SVG file"))))

(deftest svg-without-embedded-edn-is-refused
  (let [res (run-cli [(str proc-util/repo-root "/test/fixtures/plain.svg") "--no-open"])]
    (is (= 1 (:exit res)))
    (is (str/starts-with? (:err res) "simpleviz: no embedded simpleviz EDN found") (:err res))))

(deftest export-args-parse
  (is (= {:in "g.edn" :suffix nil :out "g.png" :format "png" :theme nil :force false}
         (cli/parse-export-args ["g.edn" "g.png"])))
  (is (= {:in "g.edn" :suffix "next" :out "d.SVG" :format "svg" :theme "nord" :force true}
         (cli/parse-export-args ["g.edn" "next" "d.SVG" "--theme" "nord" "--force"])))
  (is (re-find #"usage" (:error (cli/parse-export-args ["g.edn"]))))
  (is (re-find #"usage" (:error (cli/parse-export-args ["g.edn" "g.png" "--theme"]))))
  (is (re-find #"\.png or \.svg" (:error (cli/parse-export-args ["g.edn" "g.jpg"]))))
  (is (re-find #"unknown theme: neon" (:error (cli/parse-export-args ["g.edn" "g.png" "--theme" "neon"]))))
  (is (re-find #"unknown option: --nope" (:error (cli/parse-export-args ["g.edn" "g.png" "--nope"]))))
  (is (re-find #"invalid suffix" (:error (cli/parse-export-args ["g.edn" "a/b" "g.png"])))))

(deftest export-refuses-before-any-browser
  (with-tmp
    (fn [tmp]
      (spit (str (fs/path tmp "g.edn")) "{:nodes {:a {}}}")
      (spit (str (fs/path tmp "g.png")) "x")
      (let [res (run-cli ["export" "g.edn" "g.png"] :dir tmp)]
        (is (= 1 (:exit res)))
        (is (= "simpleviz: g.png already exists (--force overwrites)" (str/trim (:err res)))))
      (let [res (run-cli ["export" "nope.edn" "x.png"] :dir tmp)]
        (is (= "simpleviz: file not found: nope.edn" (str/trim (:err res)))))
      (let [res (run-cli ["export" "g.edn" "next" "x.png"] :dir tmp)]
        (is (str/includes? (:err res) "g-next.edn not found")))
      (let [res (run-cli ["export" "g.edn" "x.gif"] :dir tmp)]
        (is (= 1 (:exit res)))
        (is (str/includes? (:err res) "the output must end in .png or .svg"))))))

(deftest export-failures-of-any-kind-are-one-line-messages
  ;; a "browser" that announces DevTools on a port nobody listens on
  (with-tmp
    (fn [tmp]
      (let [fake (str (fs/path tmp "fake-browser"))]
        (spit fake "#!/bin/sh\necho 'DevTools listening on ws://127.0.0.1:9/devtools/browser/x' >&2\nsleep 30\n")
        (fs/set-posix-file-permissions fake "rwx------")
        (spit (str (fs/path tmp "g.edn")) "{:nodes {:a {}}}")
        (let [res (run-cli ["export" "g.edn" "g.png"] :dir tmp :env {"SIMPLEVIZ_BROWSER" fake})]
          (is (= 1 (:exit res)))
          (is (str/starts-with? (:err res) "simpleviz: ") (:err res))
          (is (= 1 (count (str/split-lines (str/trim (:err res))))) (:err res)))))))

(deftest export-of-an-export-without-edn-is-refused
  (let [res (run-cli ["export" (str proc-util/repo-root "/test/fixtures/plain.svg") "out.png"])]
    (is (= 1 (:exit res)))
    (is (str/starts-with? (:err res) "simpleviz: no embedded simpleviz EDN found") (:err res))))

(deftest check-exits-1-and-prints-each-problem
  (with-tmp
    (fn [tmp]
      (spit (str (fs/path tmp "warn.edn")) "{:nodes {:a {}} :edges {[:a :zz] {}} :boxes {:b {:components #{:nope}}}}")
      (spit (str (fs/path tmp "broken.edn")) "{:nodes {:a {}")
      (let [res (run-cli ["check" "warn.edn"] :dir tmp)]
        (is (= 1 (:exit res)))
        (is (= 2 (count (filter #(str/starts-with? % "warning: ") (str/split-lines (:out res)))))))
      (let [res (run-cli ["check" "broken.edn"] :dir tmp)]
        (is (= 1 (:exit res)))
        (is (str/starts-with? (:out res) "error: "))))))

(deftest check-takes-a-file-name-starting-with-a-dash
  (with-tmp
    (fn [tmp]
      (spit (str (fs/path tmp "-g.edn")) "{:nodes {:a {}}}")
      (let [res (run-cli ["check" "-g.edn"] :dir tmp)]
        (is (= 0 (:exit res)) (str (:out res) (:err res)))
        (is (= "ok" (str/trim (:out res))))))))

(deftest check-rejects-wrong-arg-count
  (doseq [args [["check"] ["check" "a.edn" "b.edn"]]]
    (let [res (run-cli args)]
      (is (= 1 (:exit res)))
      (is (str/starts-with? (:err res) "usage: simpleviz") (:err res)))))

(defn- serve-cli
  "Start the CLI with `args` in `dir` as a background process."
  [args dir]
  (proc-util/start (into ["bb" "--config" (str proc-util/repo-root "/bb.edn") "-m" "cli"] args)
                   :dir dir))

(def url-line #"^simpleviz: (http://localhost:(\d+))$")

(deftest two-file-form-is-rejected
  (let [res (run-cli [(str proc-util/repo-root "/examples/demo.edn")
                      (str proc-util/repo-root "/examples/demo-next.edn")])]
    (is (= 1 (:exit res)))
    (is (str/includes? (:err res) "two-file compare was replaced"))))

(deftest missing-fork-names-the-fork
  (let [res (run-cli [(str proc-util/repo-root "/examples/demo.edn") "nope"])]
    (is (= 1 (:exit res)))
    (is (str/includes? (:err res) "demo-nope.edn not found — create it with: simpleviz fork"))))

(deftest invalid-suffix-is-refused
  (let [res (run-cli [(str proc-util/repo-root "/examples/demo.edn") "a/b"])]
    (is (= 1 (:exit res)))
    (is (str/includes? (:err res) "invalid suffix: a/b"))))

(deftest png-without-embedded-edn-is-refused
  (let [res (run-cli [(str proc-util/repo-root "/test/fixtures/plain-1x1.png") "--no-open"])]
    (is (= 1 (:exit res)))
    (is (str/starts-with? (:err res) "simpleviz: no embedded simpleviz EDN found") (:err res))))

(deftest missing-graph-is-refused
  (let [res (run-cli ["nope.edn"])]
    (is (= 1 (:exit res)))
    (is (= "simpleviz: file not found: nope.edn" (str/trim (:err res))))))

(deftest serve-prints-the-url-and-answers-from-the-classpath
  (with-tmp
    (fn [tmp]
      (fs/copy (str proc-util/repo-root "/examples/demo.edn") (fs/path tmp "demo.edn"))
      (let [proc (serve-cli ["demo.edn" "--no-open"] tmp)]
        (try
          (let [[_ url port] (proc-util/await-line proc url-line 30000)]
            (is (some? url) "printed simpleviz: http://localhost:<port>")
            (when url
              (is (<= 7370 (parse-long port) 7469))
              (is (str/includes? (slurp url) "<html"))
              (is (= {"error" nil "warnings" []}
                     (json/parse-string (slurp (str url "/api/errors")))))))
          (finally (p/destroy-tree proc)))))))

(deftest a-folder-named-like-the-suffix-does-not-hijack-it
  (with-tmp
    (fn [tmp]
      (fs/copy (str proc-util/repo-root "/examples/demo.edn") (fs/path tmp "demo.edn"))
      (fs/copy (str proc-util/repo-root "/examples/demo-next.edn") (fs/path tmp "demo-next.edn"))
      (fs/copy-tree (str proc-util/repo-root "/examples/api") (fs/path tmp "api"))
      (fs/create-dir (fs/path tmp "next"))
      (let [proc (serve-cli ["demo.edn" "next" "--no-open"] tmp)]
        (try
          (is (some? (proc-util/await-line proc url-line 30000)))
          (finally (p/destroy-tree proc)))))))

(deftest example-files-match-the-examples-folder
  ;; the files git knows under examples/ (tracked or new, not ignored): a
  ;; git-ignored local file there is nobody else's example
  (let [out (:out (p/shell {:out :string :dir proc-util/repo-root}
                           "git" "ls-files" "--cached" "--others" "--exclude-standard" "--" "examples"))]
    (is (= (set (map #(subs % (count "examples/")) (remove str/blank? (str/split-lines out))))
           (set cli/example-files)))))

(deftest demo-copies-the-examples-and-serves-the-comparison
  (with-tmp
    (fn [tmp]
      (let [proc (serve-cli ["demo" "--no-open"] tmp)]
        (try
          (let [[_ dir] (proc-util/await-line proc #"^simpleviz: demo files in (.+)$" 30000)
                [_ url] (proc-util/await-line proc url-line 30000)]
            (is (some? dir))
            (is (some? url))
            (when dir
              (doseq [f cli/example-files]
                (is (fs/exists? (fs/path dir f)) f)))
            (when url
              (is (= {"error" nil "warnings" []}
                     (json/parse-string (slurp (str url "/api/errors"))))))
            (when dir (fs/delete-tree dir)))
          (finally (p/destroy-tree proc)))))))

(deftest a-non-startup-check-failure-writes-a-crash-report
  (with-tmp
    (fn [tmp]
      (let [form (str "(require 'cli 'serve) "
                      "(with-redefs [serve/start! (fn [_] (throw (RuntimeException. \"boom\")))] "
                      "  (cli/-main \"" proc-util/repo-root "/examples/demo.edn\" \"--no-open\"))")
            res (select-keys
                 (p/shell {:out :string :err :string :continue true
                          :extra-env {"SIMPLEVIZ_HOME" (str tmp)}}
                         "bb" "--config" (str proc-util/repo-root "/bb.edn") "-e" form)
                 [:out :err :exit])
            logs (fs/glob (fs/path tmp "logs") "crash-*.log")]
        (is (= 1 (:exit res)) (:err res))
        (is (= 1 (count logs)) (str logs))
        (when (= 1 (count logs))
          (is (str/includes? (slurp (str (first logs))) "boom")))))))

(deftest a-suffix-that-looks-like-a-flag-is-an-unknown-option
  ;; `--noopen` used to become a suffix: "g---noopen.edn not found" (#98)
  (with-tmp
    (fn [tmp]
      (spit (str (fs/path tmp "g.edn")) "{:nodes {:a {}}}")
      (doseq [args [["g.edn" "--noopen"] ["g.edn" "next" "--noopen"]
                    ["fork" "g.edn" "--noopen"] ["promote" "g.edn" "-x"]]]
        (let [res (run-cli args :dir tmp)]
          (is (= 1 (:exit res)) (pr-str args))
          (is (= (str "simpleviz: unknown option: " (last args) " (see simpleviz --help)")
                 (str/trim (:err res)))
              (pr-str args))))
      (is (= ["g.edn"] (map (comp str fs/file-name) (fs/list-dir tmp))) "no fork was written"))))

(deftest demo-with-an-example-missing-says-so
  ;; a broken build used to surface as a babashka stack trace (#98)
  (let [res (p/shell {:out :string :err :string :continue true}
                     "bb" "--config" (str proc-util/repo-root "/bb.edn") "-e"
                     (str "(require 'cli) (alter-var-root #'cli/example-files conj \"nope.edn\")"
                          " (cli/-main \"demo\" \"--no-open\")"))]
    (is (= 1 (:exit res)))
    (is (= (str "simpleviz: example missing from the classpath: nope.edn"
                " — the install looks incomplete; reinstall simpleviz")
           (str/trim (:err res))))))
