(ns simpleviz.hit-test
  (:require ["node:test" :refer [test]]
            ["node:assert/strict$default" :as assert]
            [simpleviz.hit :refer [client->graph hit-test hover-title hover-tip]]))

(defn scene [items] {:items items :width 500 :height 300})

(def node-a {:kind "node" :id "n:a" :x 100 :y 100 :w 60 :h 30})
(def edge-e {:kind "edge" :id "e0"
             :sections [[{:x 0 :y 200} {:x 300 :y 200}]]})
(def outer-box {:kind "box" :id "b:outer" :x 50 :y 50 :w 300 :h 220 :title-h 28})
(def inner-box {:kind "box" :id "b:inner" :x 80 :y 150 :w 120 :h 80 :title-h 28})

(test "client->graph inverts the view transform"
  (fn []
    (let [p (client->graph {:x 100 :y 50 :k 2} 140 90)]
      (assert/deepEqual p {:x 20 :y 20}))))

(test "node beats edge beats box"
  (fn []
    (let [s (scene [outer-box edge-e node-a])]
      (assert/equal (:id (hit-test s {:x 110 :y 110} 6)) "n:a")
      (assert/equal (:id (hit-test s {:x 250 :y 202} 6)) "e0")
      (assert/equal (:id (hit-test s {:x 60 :y 60} 6)) "b:outer"))))

(test "edge tolerance respected"
  (fn []
    (let [s (scene [edge-e])]
      (assert/equal (:id (hit-test s {:x 150 :y 205} 6)) "e0")
      (assert/ok (nil? (hit-test s {:x 150 :y 205} 3))))))

(test "box interior selects nothing; header and border bands select the box"
  (fn []
    (let [s (scene [outer-box])]
      (assert/equal (:id (hit-test s {:x 200 :y 60} 6)) "b:outer")   ; header strip
      (assert/equal (:id (hit-test s {:x 52 :y 150} 6)) "b:outer")   ; left band
      (assert/equal (:id (hit-test s {:x 348 :y 150} 6)) "b:outer")  ; right band
      (assert/equal (:id (hit-test s {:x 200 :y 268} 6)) "b:outer")  ; bottom band
      (assert/ok (nil? (hit-test s {:x 200 :y 150} 6))))))            ; interior

(test "nested boxes: innermost header wins"
  (fn []
    (let [s (scene [outer-box inner-box])]
      (assert/equal (:id (hit-test s {:x 100 :y 160} 6)) "b:inner")
      (assert/equal (:id (hit-test s {:x 200 :y 60} 6)) "b:outer"))))

(test "innermost box wins when hit zones overlap"
  (fn []
    (let [top {:kind "box" :id "b:top" :x 60 :y 52 :w 120 :h 60 :title-h 28}
          s (scene [outer-box top])]
      (assert/equal (:id (hit-test s {:x 100 :y 60} 6)) "b:top"))))

(def labeled-edge {:kind "edge" :id "e1"
                   :sections [[{:x 0 :y 300} {:x 300 :y 300}]]})
(def e1-label {:kind "edge-label" :id "e1-label" :edge-id "e1"
               :x 120 :y 280 :w 60 :h 14})

(test "labeled edge selects via its label, not its line"
  (fn []
    (let [s (scene [labeled-edge e1-label])]
      (assert/equal (:id (hit-test s {:x 150 :y 287} 6)) "e1")
      (assert/equal (:kind (hit-test s {:x 150 :y 287} 6)) "edge")
      (assert/ok (nil? (hit-test s {:x 150 :y 301} 6))))))

(test "label hit zone has a small padding"
  (fn []
    (let [s (scene [labeled-edge e1-label])]
      (assert/equal (:id (hit-test s {:x 118 :y 278} 6)) "e1"))))

(test "unlabeled edge still selects via its line"
  (fn []
    (let [s (scene [edge-e])]
      (assert/equal (:id (hit-test s {:x 150 :y 202} 6)) "e0"))))

(test "collapse button hit when zoomed in; header when zoomed out"
  (fn []
    (let [s (scene [outer-box])
          btn-p {:x 330 :y 60}]           ; button rect: x 328-343, y 57-72
      (let [it (hit-test s btn-p 6 1.0)]
        (assert/equal (:kind it) "collapse-button")
        (assert/equal (:box-id it) "b:outer"))
      ;; zoomed far out: button not drawn, so the same point is a header hit
      (assert/equal (:kind (hit-test s btn-p 40 0.2)) "box"))))


(test "collapsed box is clickable across its whole area"
  (fn []
    (let [cb {:kind "box" :id "b:c" :x 0 :y 0 :w 100 :h 60 :title-h 28 :collapsed true}]
      (assert/equal (:id (hit-test (scene [cb]) {:x 50 :y 45} 6)) "b:c"))))

(test "empty box shells expose no collapse button"
  (fn []
    (let [ghost {:kind "box" :id "b:ghost" :x 50 :y 50 :w 100 :h 30
                 :title-h 28 :collapsed true :empty true}
          s (scene [ghost])
          it (hit-test s {:x 130 :y 60} 6 1.0)] ; would-be button rect: x 128-143, y 57-72
      (assert/equal (:kind it) "box")
      (assert/equal (:id it) "b:ghost"))))

(test "hover-title shows the referenceable id per kind"
  (fn []
    (assert/equal (hover-title {:kind "node" :id "n:web"}) "web")
    (assert/equal (hover-title {:kind "box" :id "b:backend"}) "backend")
    (assert/equal (hover-title {:kind "collapse-button" :box-id "b:backend"}) "backend")
    (assert/equal (hover-title {:kind "edge" :id "e0" :source "web" :target "api"})
                  "[web api]")
    (assert/ok (nil? (hover-title nil)))))

(test "hover-tip is headed by the display name, which leaves the attrs"
  (fn []
    (let [tip (hover-tip {:kind "node" :id "n:web" :name "Web"
                          :attrs {:name "Web" :type "svc" :state "done"}})]
      (assert/equal (:title tip) "Web")
      (assert/deepEqual (:attrs tip) [["type" "svc"] ["state" "done"]]))
    ;; a node without :name is displayed under its key
    (assert/equal (:title (hover-tip {:kind "node" :id "n:web" :name "web" :attrs {}})) "web")
    (assert/ok (nil? (hover-tip nil)))))

(test "hover-tip falls back to the referenceable id for a nameless item"
  (fn []
    (assert/equal (:title (hover-tip {:kind "edge" :id "e0" :name "" :source "web" :target "api"
                                      :attrs {}}))
                  "[web api]")
    (let [tip (hover-tip {:kind "collapse-button" :box-id "b:x"})]
      (assert/equal (:title tip) "x")
      (assert/deepEqual (:attrs tip) []))))

(test "hover-tip leaves out the attrs the canvas already shows, like the inspector"
  (fn []
    (assert/deepEqual
     (:attrs (hover-tip {:kind "edge" :id "e0" :name "" :source "a" :target "b"
                         :attrs {:nodes ["a" "b"] :direction "->" :type "http"}}))
     [["type" "http"]])
    (assert/deepEqual
     (:attrs (hover-tip {:kind "box" :id "b:g" :name "g"
                         :attrs {:components ["a"] :type "zone"}}))
     [["type" "zone"]])
    ;; the same keys on a node are ordinary attributes
    (assert/deepEqual
     (:attrs (hover-tip {:kind "node" :id "n:a" :name "a" :attrs {:components 3}}))
     [["components" 3]])))

(def world-model-node {:kind "node" :id "n:eo" :x 0 :y 0 :w 300 :h 320 :name "EO"
                   :world-model? true :attrs {"world-model" {"satellites" {}} "owner" "ops"}})

(test "a world-model node's control-bar buttons hit ahead of the node, when legible"
  (fn []
    (let [s (scene [world-model-node])
          ;; the bar's first button (play) sits at x 6.., y 297..315
          b (hit-test s {:x 10 :y 300} 8 1)]
      (assert/deepEqual b {:kind "world-model-button" :node-id "n:eo" :action "play"})
      (assert/equal (hover-title b) "run / pause the clock")
      ;; the globe itself is the node
      (assert/equal (:id (hit-test s {:x 150 :y 150} 8 1)) "n:eo")
      ;; zoomed out past legibility: the node
      (assert/equal (:id (hit-test s {:x 10 :y 300} 80 0.1)) "n:eo"))))

(test "a world-model node's tooltip leaves its :world-model out"
  (fn []
    (assert/deepEqual (:attrs (hover-tip world-model-node)) [["owner" "ops"]])))

(test "the tooltip leaves out a :text the element already shows"
  (fn []
    (assert/deepEqual (:attrs (hover-tip (assoc node-a :name "A" :attrs {"text" "hi" "owner" "ops"}
                                                :text-lines ["hi"])))
                      [["owner" "ops"]])
    ;; a collapsed box doesn't show it: the tooltip does
    (assert/deepEqual (:attrs (hover-tip {:kind "box" :id "b:g" :name "G" :attrs {"text" "hi"} :text-lines []}))
                      [["text" "hi"]])))
