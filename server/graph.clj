(ns graph
  "Parsed EDN -> normalized, render-ready graph. Shape checks via malli
  (lenient: humanized warning + skip), semantics in plain Clojure. Never
  throws on any input value."
  (:require [clojure.string :as str]
            [malli.core :as m]
            [malli.error :as me]
            [themes]))

(def ^:private EdgeShape
  (m/schema [:map [:nodes [:tuple [:or :string :keyword] [:or :string :keyword]]]]))

;; compiled once: m/explain per edge re-interprets the schema and costs
;; ~300x the compiled validator, which dominated normalize at 10k nodes
(def ^:private edge-shape-valid? (m/validator EdgeShape))
(def ^:private edge-shape-explainer (m/explainer EdgeShape))

(def ^:private directions
  {:-> :-> :<- :<- :<-> :<-> :- :-
   "->" :-> "<-" :<- "<->" :<-> "-" :-})

(defn- ident->str
  "Coerce a node/box/component identifier to a display string. Keywords lose
  their leading colon but keep their namespace (matching the old
  EDN->cheshire->frontend pipeline); anything else falls back to str."
  [x]
  (if (keyword? x) (subs (str x) 1) (str x)))

(defn- coerce-str [x fallback]
  (ident->str (if (nil? x) fallback x)))

(defn- edge-shape-error
  "nil when the edge matches EdgeShape, else the humanized malli
  explanation rendered as a string; the explainer only runs on failures."
  [e]
  (when-not (edge-shape-valid? e)
    (pr-str (me/humanize (edge-shape-explainer e)))))

(defn- top-level [raw warn! k pred coerce-empty msg]
  (let [v (get raw k)]
    (cond (nil? v) coerce-empty
          (pred v) (if (set? v) (vec v) v)
          :else (do (warn! msg) coerce-empty))))

(defn- boxes-map->seq
  "Map-form boxes {:id {..}} -> uniform {:name :label :attrs} entries: the
  key is the box identity, :name in the value is the display label
  (defaulting to the key), like nodes. Sorted by key so contested
  memberships resolve deterministically (EDN maps above 8 entries do not
  preserve file order)."
  [m warn!]
  (->> (sort-by (fn [[k _]] (ident->str k)) (seq m))
       (keep (fn [[k v]]
               (let [nm (ident->str k)]
                 (cond
                   (nil? v) {:name nm :label nm :attrs {}}
                   (map? v) {:name nm :label (coerce-str (:name v) nm) :attrs v}
                   :else (do (warn! (str "box \"" nm "\": value must be a map, skipped")) nil)))))
       vec))

(defn- boxes-vec->seq
  "Pre-v2 vector-form boxes [{:name ..}] -> uniform {:name :label :attrs}
  entries; :name is both the identity and the display label."
  [boxes-in warn!]
  (->> boxes-in
       (map-indexed
        (fn [i b]
          (if-not (map? b)
            (do (warn! (str "box " i ": not a map, skipped")) nil)
            (let [nm (coerce-str (:name b) "")]
              (if (str/blank? nm)
                (do (warn! (str "box " i ": missing :name, skipped")) nil)
                {:name nm :label nm :attrs b})))))
       (remove nil?)
       vec))

(defn- edges-map->seq
  "Map-form edges {[:a :b] {..}} -> edge maps with the key as :nodes.
  Sorted by the coerced endpoint pair so edge ids stay stable regardless
  of EDN map iteration order. The sort key is precomputed per entry:
  sort-by recomputes its keyfn on every comparison, which made pr-str
  dominate this function at 10k nodes."
  [m warn!]
  (->> (seq m)
       (mapv (fn [[k v]]
               [(if (vector? k) (pr-str (mapv ident->str k)) (pr-str k)) k v]))
       (sort-by first)
       (keep (fn [[_ k v]]
               (cond
                 (not (and (vector? k) (= 2 (count k))))
                 (do (warn! (str "edge key " (pr-str k) ": must be a 2-element vector, skipped")) nil)

                 (and (some? v) (not (map? v)))
                 (do (warn! (str "edge " (pr-str k) ": value must be a map, skipped")) nil)

                 :else
                 (let [v (or v {})]
                   (when (some? (:nodes v))
                     (warn! (str "edge " (pr-str k) ": :nodes in value ignored (the key defines the endpoints)")))
                   (assoc v :nodes (mapv ident->str k))))))
       vec))

(defn- warn-reversed-pairs!
  "One warning per unordered pair that appears in both orientations —
  usually the same connection written twice."
  [edges-in warn!]
  (let [pairs (keep (fn [e] (when (and (map? e) (vector? (:nodes e)) (= 2 (count (:nodes e))))
                              (mapv ident->str (:nodes e))))
                    edges-in)
        present (set pairs)]
    (doseq [[a b] (distinct pairs)]
      (when (and (not= a b) (contains? present [b a]) (pos? (compare a b)))
        (warn! (str "edges [" b " " a "] and [" a " " b "] describe the same connection"))))))

(defn- build-nodes [nodes-in warn!]
  (reduce-kv
   (fn [acc k v]
     (let [k (ident->str k)
           attrs (if (map? v) v {})]
       (when-not (or (nil? v) (map? v))
         (warn! (str "node \"" k "\": attributes must be a map, using {}")))
       (assoc acc k {:id k
                     :name (coerce-str (:name attrs) k)
                     :type (coerce-str (:type attrs) "")
                     :attrs attrs})))
   {} nodes-in))

(defn- build-edges [edges-in nodes box-names warn!]
  (let [warned (atom #{})
        resolve-end
        (fn [x]
          (let [is-node (contains? nodes x)
                is-box (contains? box-names x)]
            (cond
              (and is-node is-box)
              (do (when-not (contains? @warned x)
                    (warn! (str "\"" x "\" names both a node and a box; edges get the node"))
                    (swap! warned conj x))
                  {:name x :eid (str "n:" x)})
              is-node {:name x :eid (str "n:" x)}
              is-box {:name x :eid (str "b:" x)}
              :else nil)))]
    (->> edges-in
         (map-indexed
          (fn [i e]
            (if-not (map? e)
              (do (warn! (str "edge " i ": not a map, skipped")) nil)
              (if-let [humanized (edge-shape-error e)]
                (do (warn! (str "edge " i ": :nodes must be a vector of exactly 2 node names ("
                                humanized ")"))
                    nil)
                (let [[a0 b0] (:nodes e)
                      a (ident->str a0)
                      b (ident->str b0)
                      ra (resolve-end a)
                      rb (resolve-end b)
                      missing (into [] (keep (fn [[x r]] (when (nil? r) x)))
                                    [[a ra] [b rb]])]
                  (if (seq missing)
                    (do (warn! (str "edge " i " [" a " " b "]: unknown node or box: "
                                    (str/join ", " missing)))
                        nil)
                    (let [dir0 (:direction e)
                          dir (cond
                                (nil? dir0) :-
                                (contains? directions dir0) (get directions dir0)
                                :else (do (warn! (str "edge " i ": unknown direction "
                                                      (pr-str dir0)
                                                      ", treating as undirected"))
                                          :-))
                          [src tgt] (if (= dir :<-) [rb ra] [ra rb])]
                      {:id (str "e" i)
                       :source (:name src)
                       :target (:name tgt)
                       :source-id (:eid src)
                       :target-id (:eid tgt)
                       :arrows {:source (= dir :<->) :target (not= dir :-)}
                       :name (coerce-str (:name e) "")
                       :type (coerce-str (:type e) "")
                       :attrs e})))))))
         (remove nil?)
         vec)))

(defn- build-boxes
  "Uniform {:name :label :attrs} entries -> normalized box maps. :name is
  the identity (referenced by edges and :components), :label the display
  name."
  [entries warn!]
  (first
   (reduce
    (fn [[acc seen] {:keys [name label attrs]}]
      (cond
        (str/blank? name)
        (do (warn! (str "box " (count acc) ": missing :name, skipped"))
            [acc seen])

        (contains? seen name)
        (do (warn! (str "box \"" name "\": duplicate name, later definition skipped"))
            [acc seen])

        :else
        (let [components
              (let [c (:components attrs)]
                (cond (nil? c) []
                      (or (sequential? c) (set? c)) (vec c)
                      :else (do (warn! (str "box \"" name
                                            "\": :components must be a collection, skipped"))
                                [])))]
          [(conj acc {:id (str "b:" name)
                      :name name
                      :label label
                      :type (coerce-str (:type attrs) "")
                      :components components
                      :attrs attrs})
           (conj seen name)])))
    [[] #{}] entries)))

(defn- resolve-membership
  "First box in file order wins; components become prefixed ids."
  [boxes nodes warn!]
  (let [box-names (set (map :name boxes))]
    (reduce
     (fn [[bs parents] box]
       (let [[kept parents]
             (reduce
              (fn [[kept parents] c]
                (let [c (ident->str c)
                      is-node (contains? nodes c)
                      is-box (contains? box-names c)]
                  (cond
                    (and (not is-node) (not is-box))
                    (do (warn! (str "box \"" (:name box) "\": unknown component \"" c "\""))
                        [kept parents])

                    (and is-box (not is-node) (= c (:name box)))
                    (do (warn! (str "box \"" (:name box) "\" cannot contain itself"))
                        [kept parents])

                    :else
                    (let [_ (when (and is-node is-box)
                              (warn! (str "\"" c "\" names both a node and a box; box \""
                                          (:name box) "\" gets the node")))
                          id (if is-node (str "n:" c) (str "b:" c))]
                      (if (contains? parents id)
                        (do (warn! (str "\"" c "\" is already in box \"" (get parents id)
                                        "\"; membership in \"" (:name box) "\" ignored"))
                            [kept parents])
                        [(conj kept id) (assoc parents id (:name box))])))))
              [[] parents] (:components box))]
         [(conj bs (assoc box :components kept)) parents]))
     [[] {}] boxes)))

(defn- break-cycles [boxes parent-of warn!]
  (reduce
   (fn [[bs parents] box]
     (loop [seen #{(:name box)}
            p (get parents (str "b:" (:name box)))]
       (cond
         (nil? p) [bs parents]

         (contains? seen p)
         (let [parent-name (get parents (str "b:" (:name box)))
               child-id (str "b:" (:name box))]
           (warn! (str "box containment cycle: detaching \"" (:name box)
                       "\" from \"" parent-name "\""))
           [(mapv (fn [b]
                    (if (= (:name b) parent-name)
                      (update b :components (fn [cs] (filterv #(not= % child-id) cs)))
                      b))
                  bs)
            (dissoc parents child-id)])

         :else (recur (conj seen p) (get parents (str "b:" p))))))
   [boxes parent-of] boxes))

(defn- ancestor?
  "Is `box-name` a transitive container of the element with prefixed id?"
  [parent-of box-name id]
  (loop [p (get parent-of id)]
    (cond
      (nil? p) false
      (= p box-name) true
      :else (recur (get parent-of (str "b:" p))))))

(defn- drop-containment-edges
  "Remove edges where a box endpoint contains the other endpoint, or both
  endpoints are the same box; warns per dropped edge."
  [edges parent-of warn!]
  (filterv
   (fn [e]
     (let [s (:source-id e)
           t (:target-id e)
           s-box (when (str/starts-with? s "b:") (subs s 2))
           t-box (when (str/starts-with? t "b:") (subs t 2))]
       (cond
         (and (some? s-box) (= s t))
         (do (warn! (str "edge [" (:source e) " " (:target e)
                         "]: a box cannot connect to itself, skipped"))
             false)

         (and (some? s-box) (ancestor? parent-of s-box t))
         (do (warn! (str "edge [" (:source e) " " (:target e) "]: "
                         s-box " contains " (:target e) ", skipped"))
             false)

         (and (some? t-box) (ancestor? parent-of t-box s))
         (do (warn! (str "edge [" (:source e) " " (:target e) "]: "
                         t-box " contains " (:source e) ", skipped"))
             false)

         :else true)))
   edges))

(def ^:private css-number "[+-]?(?:\\d+\\.?\\d*|\\.\\d+)")

(def ^:private color-re
  (let [n css-number]
    (re-pattern
     (str "#(?:[0-9a-fA-F]{3,4}|[0-9a-fA-F]{6}|[0-9a-fA-F]{8})"
          ;; modern space-separated rgb()/hsl(), optional "/ alpha"
          "|(?:rgba?|hsla?)\\(\\s*" n "(?:%|deg|rad|grad|turn)?(?:\\s+" n "%?){2}(?:\\s*/\\s*" n "%?)?\\s*\\)"
          ;; legacy comma rgb()/rgba()
          "|rgba?\\(\\s*" n "%?(?:\\s*,\\s*" n "%?){2}(?:\\s*,\\s*" n "%?)?\\s*\\)"
          ;; legacy comma hsl()/hsla(): saturation and lightness need %
          "|hsla?\\(\\s*" n "(?:deg|rad|grad|turn)?\\s*,\\s*" n "%\\s*,\\s*" n "%(?:\\s*,\\s*" n "%?)?\\s*\\)"))))

(def ^:private theme-expectations
  {:color "a color (#hex, rgb(), hsl())"
   :percent "a number from 0 to 100"
   :alpha "a number from 0 to 1"})

(defn- theme-value-ok? [kind v]
  (case kind
    :color (and (string? v) (some? (re-matches color-re v)))
    :percent (and (number? v) (<= 0 v 100))
    :alpha (and (number? v) (<= 0 v 1))
    false))

(defn- theme-name
  "The built-in theme keyword a keyword or string names, else nil."
  [x]
  (when (or (keyword? x) (string? x))
    (let [k (keyword (ident->str x))]
      (when (contains? themes/THEMES k) k))))

(defn- theme-key
  "Map keys may be keywords or strings, like identifiers elsewhere."
  [k]
  (if (or (keyword? k) (string? k)) (keyword (ident->str k)) k))

(defn- resolve-theme
  "The file's :theme -> a complete theme map (all themes/KEYS), or nil
  when there is no valid file theme. One warning per problem: an unknown
  name or a non-name non-map drops the theme, an unknown :base falls back
  to light, an unknown key or bad value drops just that key."
  [v warn!]
  (cond
    (nil? v) nil

    (or (keyword? v) (string? v))
    (or (some->> (theme-name v) (get themes/THEMES))
        (do (warn! (str ":theme: unknown theme \"" (ident->str v) "\" (built-in: "
                        (str/join ", " (map name themes/NAMES)) ")"))
            nil))

    (map? v)
    (let [entries (sort-by (fn [[k _]] (str k)) (map (fn [[k x]] [(theme-key k) x]) v))
          b (or (some (fn [[k x]] (when (= k :base) x)) entries) :light)
          base (or (theme-name b)
                   (do (warn! (str ":theme: unknown base \"" (ident->str b) "\", using light"))
                       :light))]
      (reduce (fn [acc [k x]]
                (let [kind (get themes/KEY-KINDS k)]
                  (cond
                    (= k :base) acc
                    (nil? kind) (do (warn! (str ":theme: unknown key " (pr-str k) ", ignored")) acc)
                    (theme-value-ok? kind x) (assoc acc k x)
                    :else (do (warn! (str ":theme " k ": expected " (theme-expectations kind) ", ignored"))
                              acc))))
              (get themes/THEMES base)
              entries))

    :else (do (warn! ":theme must be a theme name or a map, ignoring it") nil)))

(def ^:private grid-max
  "Cells stop at index 99: huge indices made a page lay out (and route
  through) thousands of empty columns."
  100)

(defn- grid-cell
  "{:col :row :w :h} for a well-formed :grid value within grid-max
  columns and rows, else nil."
  [v]
  (when (and (vector? v) (contains? #{2 4} (count v)) (every? integer? v))
    (let [[col row w h] (if (= 2 (count v)) (conj v 1 1) v)]
      (when (and (>= col 0) (>= row 0) (>= w 1) (>= h 1)
                 (<= (+ col w) grid-max) (<= (+ row h) grid-max))
        {:col col :row row :w w :h h}))))

(defn- resolve-grids
  "Each box with its :grid as {:col :row :w :h}, or nil: only top-level
  boxes, well-formed values, and — where cells overlap — the box first
  by sorted name keeps them. Everything else warns and is ignored."
  [boxes parent-of warn!]
  (let [raw (into {} (map (fn [b] [(:name b) (get-in b [:attrs :grid])])) boxes)
        cells (into {}
                    (keep (fn [b]
                            (let [v (get raw (:name b))]
                              (when (some? v)
                                (cond
                                  (some? (get parent-of (:id b)))
                                  (do (warn! (str "box \"" (:name b) "\": :grid only applies to top-level boxes, ignored"))
                                      nil)
                                  (nil? (grid-cell v))
                                  (do (warn! (str "box \"" (:name b) "\": :grid must be [col row] or [col row w h]"
                                                  " (integers, col/row 0–99, w/h ≥ 1, within 100 columns/rows), ignored"))
                                      nil)
                                  :else [(:name b) (grid-cell v)])))))
                    boxes)
        kept (first
              (reduce (fn [[kept taken] nm]
                        (let [{:keys [col row w h]} (get cells nm)
                              ks (for [c (range col (+ col w)) r (range row (+ row h))] [c r])]
                          (if-let [other (some taken ks)]
                            (do (warn! (str "box \"" nm "\": :grid " (pr-str (get raw nm))
                                            " overlaps box \"" other "\", ignored"))
                                [kept taken])
                            [(assoc kept nm (get cells nm)) (into taken (map (fn [k] [k nm])) ks)])))
                      [{} {}]
                      (sort (keys cells))))]
    (mapv (fn [b] (assoc b :grid (get kept (:name b)))) boxes)))

(def LAYOUTS
  "The layout algorithms a file's top-level :layout can pick."
  #{"layered" "compact"})

(defn- resolve-layout
  "The file's :layout as a layout name, nil when absent; an unknown one
  warns and is dropped (the viewer's choice applies)."
  [v warn!]
  (cond
    (nil? v) nil
    (and (or (keyword? v) (string? v)) (contains? LAYOUTS (ident->str v))) (ident->str v)
    :else (do (warn! (str ":layout: unknown layout " (pr-str v) " (layered or compact), ignored"))
              nil)))

(defn normalize [raw]
  (let [warnings (atom [])
        warn! (fn [msg] (swap! warnings conj msg))
        raw (if (map? raw)
              raw
              (do (when (some? raw) (warn! "root must be a map, ignoring content")) {}))
        nodes-in (top-level raw warn! :nodes map? {} ":nodes must be a map, ignoring it")
        edges-in (let [e (:edges raw)]
                   (if (map? e)
                     (edges-map->seq e warn!)
                     (top-level raw warn! :edges #(or (sequential? %) (set? %)) []
                                ":edges must be a map or vector, ignoring it")))
        boxes-in (let [b (:boxes raw)]
                   (if (map? b)
                     (boxes-map->seq b warn!)
                     (boxes-vec->seq
                      (top-level raw warn! :boxes #(or (sequential? %) (set? %)) []
                                 ":boxes must be a map or vector, ignoring it")
                      warn!)))
        _ (warn-reversed-pairs! edges-in warn!)
        nodes (build-nodes nodes-in warn!)
        boxes0 (build-boxes boxes-in warn!)
        edges0 (build-edges edges-in nodes (set (map :name boxes0)) warn!)
        [boxes1 parents1] (resolve-membership boxes0 nodes warn!)
        [boxes parent-of] (break-cycles boxes1 parents1 warn!)
        edges (drop-containment-edges edges0 parent-of warn!)
        boxes (resolve-grids boxes parent-of warn!)
        theme (resolve-theme (:theme raw) warn!)
        layout (resolve-layout (:layout raw) warn!)
        ;; the built-in's name when the file names one (the page's theme
        ;; menu can rewrite that); a :theme map is custom and has none
        built-in (when (and (some? theme) (not (map? (:theme raw))))
                   (name (theme-name (:theme raw))))]
    (cond-> {:nodes nodes
             :edges edges
             :boxes boxes
             :parent-of parent-of
             :warnings @warnings}
      (some? theme) (assoc :theme theme)
      (some? layout) (assoc :layout layout)
      (some? built-in) (assoc :theme-name built-in))))
