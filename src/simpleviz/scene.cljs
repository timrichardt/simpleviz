(ns simpleviz.scene
  (:require [simpleviz.editor :refer [ref-of]]
            [simpleviz.world-model :as wm]))

;; layout + graph + type-color slots -> flat, back-to-front draw list with absolute
;; coordinates: boxes, edges, edge labels, nodes. Pure data; the canvas
;; painter draws it and hit-testing walks it. No DOM.
;;
;; Hot path for large graphs: accumulation uses mutating JS arrays/Maps —
;; squint's persistent conj/assoc copy on every call, which turns 10k-item
;; builds quadratic (guarded by a perf test).

(def TITLE-H 28)

;; shared by the painter (draw threshold) and hit-testing (click zone):
;; below this rendered font height, text/labels/arrowheads and the box
;; hide-button are neither drawn nor clickable
(def TEXT-MIN-PX 4.5)

;; hide-button square in the box header, offsets from the top-right corner
(def HIDE-BTN-SIZE 15)
(def HIDE-BTN-RIGHT 22)
(def HIDE-BTN-TOP 7)

(def ^:private BBOX-PAD 10)

;; the :state values a node shows as a corner mark; anything else stays an
;; ordinary attribute
(def STATES #{"new" "in-progress" "blocked" "done"})

(defn- node-state [node]
  (let [s (:state (:attrs node))]
    (when (contains? STATES s) s)))

(defn- color-idx
  "The element's type-color slot from idx-by-type, nil when untyped. The
  painter looks the color up in the current theme, so a theme change
  never invalidates a scene."
  [el idx-by-type]
  (when (pos? (.-length (:type el)))
    (get idx-by-type (:type el))))

(defn- section-points [sec]
  (into [(:startPoint sec)]
        (conj (vec (or (:bendPoints sec) [])) (:endPoint sec))))

(defn- offset-pts [pts origin]
  (mapv (fn [p] {:x (+ (:x p) (:x origin)) :y (+ (:y p) (:y origin))}) pts))

(defn- rect-bbox [x y w h]
  {:x0 (- x BBOX-PAD) :y0 (- y BBOX-PAD)
   :x1 (+ x w BBOX-PAD) :y1 (+ y h BBOX-PAD)})

(defn- points-bbox [pts]
  (loop [i 0
         x0 js/Infinity y0 js/Infinity
         x1 (- js/Infinity) y1 (- js/Infinity)]
    (if (< i (.-length pts))
      (let [p (nth pts i)]
        (recur (inc i)
               (js/Math.min x0 (:x p)) (js/Math.min y0 (:y p))
               (js/Math.max x1 (:x p)) (js/Math.max y1 (:y p))))
      {:x0 (- x0 BBOX-PAD) :y0 (- y0 BBOX-PAD)
       :x1 (+ x1 BBOX-PAD) :y1 (+ y1 BBOX-PAD)})))

(defn visible?
  "Does the item's bounding box intersect the view rect {:x0 :y0 :x1 :y1}?"
  [item vr]
  (let [bb (:bbox item)]
    (and (< (:x0 bb) (:x1 vr)) (> (:x1 bb) (:x0 vr))
         (< (:y0 bb) (:y1 vr)) (> (:y1 bb) (:y0 vr)))))

(defn- pair-fields
  "The pair data a node or box item carries: its payload :pairs, whether
  it has any, and whether one of them is broken."
  [el]
  (let [ps (or (:pairs el) [])]
    {:pairs ps
     :pair? (pos? (.-length ps))
     :pair-problem? (boolean (some (fn [p] (some? (:problem p))) ps))}))

(defn build-scene [{:keys [layout graph colors]}]
  (let [boxes (js/Array.)
        nodes (js/Array.)
        origins (js/Map.)]
    ((fn walk [parent ox oy]
       (doseq [child (or (:children parent) [])]
         (let [x (+ ox (:x child))
               y (+ oy (:y child))]
           (if (.startsWith (:id child) "b:")
             (let [box (get (:boxes-by-name graph) (.slice (:id child) 2))
                   ;; genuinely empty (unlike a user-collapsed shell, which
                   ;; also has no components but carries :collapsed): draw
                   ;; collapsed-style, but there is nothing to toggle
                   empty? (and (not (:collapsed box))
                               (zero? (.-length (or (:components box) []))))]
               (.push boxes (merge {:kind "box" :id (:id child)
                                    :x x :y y :w (:width child) :h (:height child)
                                    :title-h TITLE-H
                                    :collapsed (or (:collapsed box) empty?)
                                    :empty empty?
                                    :bbox (rect-bbox x y (:width child) (:height child))
                                    :color-idx (color-idx box (:box colors))
                                    :name (or (:label box) (:name box)) :type (:type box)
                                    :attrs (:attrs box)
                                    :md-ref? (some? (ref-of box :md-ref))
                                    :diff (:diff box) :changed (:changed box) :diff-inside (:diff-inside box)}
                             (pair-fields box)))
               (.set origins (:id child) {:x x :y y})
               (walk child x y))
             (let [node (get (:nodes graph) (.slice (:id child) 2))
                   ms (wm/world-model-of (:attrs node))]
               (.push nodes (merge {:kind "node" :id (:id child)
                                    :x x :y y :w (:width child) :h (:height child)
                                    :bbox (rect-bbox x y (:width child) (:height child))
                                    :color-idx (color-idx node (:node colors))
                                    :name (:name node) :type (:type node)
                                    :attrs (:attrs node)
                                    :ref? (some? (ref-of node))
                                    :md-ref? (some? (ref-of node :md-ref))
                                    :world-model? (some? ms)
                                    ;; parsed once here, painted every frame
                                    :world-model (when (some? ms) (wm/parse ms))
                                    :state (node-state node)
                                    :diff (:diff node) :changed (:changed node)}
                             (pair-fields node))))))))
     layout 0 0)
    (let [edges-by-id (let [m (js/Map.)]
                        (doseq [e (:edges graph)] (.set m (:id e) e))
                        m)
          origin-of (fn [elk-edge] (or (.get origins (:container elk-edge)) {:x 0 :y 0}))
          edge-items (js/Array.)
          label-items (js/Array.)]
      (doseq [elk-edge (or (:edges layout) [])]
        (let [e (.get edges-by-id (:id elk-edge))
              origin (origin-of elk-edge)
              sections (mapv (fn [sec] (offset-pts (section-points sec) origin))
                             (or (:sections elk-edge) []))]
          (when (and (some? e) (pos? (.-length sections)))
            (let [points (vec (apply concat sections))]
              (.push edge-items {:kind "edge" :id (:id elk-edge)
                                 :sections sections
                                 :points points
                                 :bbox (points-bbox points)
                                 :arrows (:arrows e)
                                 :source (:source e)
                                 :target (:target e)
                                 :source-id (:source-id e)
                                 :target-id (:target-id e)
                                 :name (:name e) :type (:type e) :attrs (:attrs e)
                                 :diff (:diff e) :changed (:changed e)})))
          (when-let [lbl (first (or (:labels elk-edge) []))]
            (let [lx (+ (:x lbl) (:x origin))
                  ly (+ (:y lbl) (:y origin))]
              (.push label-items {:kind "edge-label" :id (str (:id elk-edge) "-label")
                                  :edge-id (:id elk-edge)
                                  :x lx :y ly
                                  :w (:width lbl) :h (:height lbl)
                                  :bbox (rect-bbox lx ly (:width lbl) (:height lbl))
                                  :text (:text lbl)
                                  :diff (when (some? e) (:diff e))})))))
      {:items (.concat boxes edge-items label-items nodes)
       :width (or (:width layout) 0)
       :height (or (:height layout) 0)})))

(defn diff-stops
  "Cycle stops per diff status: {\"added\" [items] ...}. Nodes, boxes and
  edges whose :diff matches; collapsed shells additionally stop for every
  status in :diff-inside (deduped against their own :diff). Edge labels
  are never stops. Nil-safe for a missing scene."
  [sc]
  (let [acc {"added" (js/Array.) "modified" (js/Array.) "removed" (js/Array.)}]
    (doseq [it (or (:items sc) [])]
      (when (not= (:kind it) "edge-label")
        (let [d (:diff it)]
          (when (some? (get acc d)) (.push (get acc d) it))
          (doseq [s (or (:diff-inside it) [])]
            (when (and (not= s d) (some? (get acc s)))
              (.push (get acc s) it))))))
    acc))
